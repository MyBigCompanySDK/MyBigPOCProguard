apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'My big sdk core'
                    url = 'https://github.com/MyBigCompanySDK/MyBigPOCProguard'
                    licenses {
                        license {
                            name = 'Apache License'
                            url = 'https://github.com/MyBigCompanySDK/MyBigPOCProguard/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'TheScreamer74'
                            name = 'Thomas Huyghues'
                            email = 'thomas.huyghues@miam.tech'
                        }
                        // Add other devs here...
                    }
                    scm {
                        connection = 'https://github.com/MyBigCompanySDK/MyBigPOCProguard.git'
                        developerConnection = 'git@github.com:MyBigCompanySDK/MyBigPOCProguard.git'
                        url = "https://github.com/MyBigCompanySDK/MyBigPOCProguard/tree/main"
                    }
                }
            }
        }
    }
}

signing {
    ext["signing.keyId"] = rootProject.ext["signing.keyId"]
    ext["signing.password"] = rootProject.ext["signing.password"]
    ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

    sign publishing.publications
}
